*:**************************************************************************
*: Program file  : ICPAKSKU
*: Program desc. : PACK/SKU REPORT
*: Date          : 07/15/2008
*: System        : Aria Advantage Series.4XP
*: Module        : INVENTORY CONTROL (IC)
*: Developer     : Mariam Mazhar (MMT)
*:**************************************************************************
*: Calls : 
*:    Procedures : gfDispRe
*:    Functions  : lfAdd2Tmp() , lfGetTmpl() , lfQtyStr()
*:                 
*:**************************************************************************
*: Passed Parameters  : None
*:**************************************************************************
*: Notes   : .... 
*:**************************************************************************
*: Example : DO ICPAKSKU
*:**************************************************************************
*: This Program is due to  N037562,1[T20080515.0001]
*:**************************************************************************
*N000682,1 MMT 02/11/2013 Globalization changes[Start]
#include  R:\aria4xp\reports\ic\icpaksku.h
*N000682,1 MMT 02/11/2013 Globalization changes[End]
loOGScroll.cCROrientation='P'
IF LOOGSCROLL.llOGFltCh &&OR llClearFn
  lfCrtTemp()
  lfCollectData()
ENDIF 
xTotPack = 0      
SELECT (lcTempFile)
LOCATE 
DO gfDispRe WITH EVAL('lcRpName')

*!**************************************************************************
*! Name      : lfAdd2Tmp
*! Developer : Mariam Mazhar (MMT)
*! Date      : 07/15/2008
*! Purpose   : Add new record in tmp file.
*!**************************************************************************
*! Called from : Main PRG
*!**************************************************************************
*! Calls       : 
*!**************************************************************************
*! Passed Parameters : None
*!**************************************************************************
*! Return      : None
*!**************************************************************************
*! Example     : =lfAdd2Tmp()
*!**************************************************************************
*
FUNCTION lfAdd2Tmp

SCATTER TO laFields   
SELECT (lcTempFile)
APPEND BLANK
GATHER FROM laFields  

IF TYPE = 'S'
  IF EMPTY(PrePak)
    FOR I = 1 TO 8
      Z=STR(I,1)
      IF QTY&Z = 1
        EXIT
      ENDIF
    ENDFOR
    REPLACE Sku WITH SCALE.Sz&Z
  ELSE
    REPLACE Sku WITH 'PREPACK'
  ENDIF
ENDIF  
*!**************************************************************************
*! Name      : lfGetTmpl
*! Developer : Mariam Mazhar (MMT)
*! Date      : 07/15/2008
*! Purpose   : Returns the template pack id from SKUTMPL File. 
*!**************************************************************************
*! Called from : IcpakSku.FRX
*!**************************************************************************
*! Calls       : 
*!**************************************************************************
*! Passed Parameters : None
*!**************************************************************************
*! Return      : None
*!**************************************************************************
*! Example     : =lfGetTmpl()
*!**************************************************************************
*
FUNCTION lfGetTmpl

gfSeek('M'+Account ,'CUSTOMER')
xtotpack = 0          && Variable to store Report Total QTYs for every Style
lcSPackID = ''        && var. to store the pack id Title  
lcAlias   = SELECT()  && var. to store the current Alias

*-- get pack_id title from SKUTMPL File
SELECT SKUTMPL

IF gfSEEK('P'+CUSTOMER.SkuTmpl)  
  lcFld1=ALLTRIM(Field1)
  lcFld2=ALLTRIM(Field2)
  lcSpackid= lcFld1+' : '+SUBSTR(&lcTempFile..PACK_ID,1,Len1)+'    '+;
  IIF(Len2<>0,lcFld2+' : '+SUBSTR(&lcTempFile..PACK_ID,Len1+1,Len2),'')
ELSE   
  lcSPackID = 'Pack id : ' + &lcTempFile..PACK_ID
ENDIF

SELECT(lcAlias)  && restore Saved Alias

RETURN lcSPackID
*-- end of lfGetTmpl.

*!**************************************************************************
*! Name      : lfQtyStr
*! Developer : Mariam Mazhar (MMT)
*! Date      : 07/15/2008
*! Purpose   : Returns QTY line string in report. 
*!**************************************************************************
*! Called from : IcpakSku.FRX
*!**************************************************************************
*! Calls       : 
*!**************************************************************************
*! Passed Parameters : None
*!**************************************************************************
*! Return      : None
*!**************************************************************************
*! Example     : =lfQtyStr()
*!**************************************************************************
*
FUNCTION lfQtyStr

=gfSeek(Style,'Style')
=gfSeek('S'+Style.Scale,'Scale')

lcQtyTxt = ''  && variable to store the Scale

*-- this loop collects all Scales in one string variable separated by SPACES
FOR I=1 TO SCALE.Cnt
  Z=STR(I,1)
  lcQtyTxt=lcQtyTxt+STR(Qty&Z,6)
ENDFOR
*-- end of for loop

lcQtyTxt = lcQtyTxt + SPACE(48-LEN(lcQtyTxt)) + STR(TotQty,7)
xTotPack = xTotPack + TotQty
RETURN (lcQtyTxt)
*-- end of lfQtyStr.

*!**************************************************************************
*! Name      : lfwRepWhen
*! Developer : Mariam Mazhar (MMT)
*! Date      : 07/15/2008
*! Purpose   : When Function
*!**************************************************************************
FUNCTION lfwRepWhen
gfOpenTable('spck_hdr','spck_hdr')
gfOpenTable('Spck_lin','Spck_lin')
gfOpenTable('Style','Style')
gfOpenTable('Scale','Scale')
gfOpenTable('SKUTMPL','SKUTMPL')
gfOpenTable('Customer','Customer')

*!**************************************************************************
*! Name      : lfCrtTemp
*! Developer : Mariam Mazhar (MMT)
*! Date      : 07/15/2008
*! Purpose   : Create Temp File
*!**************************************************************************
FUNCTION lfCrtTemp
SELECT Spck_lin
DIMENSION laFileStr[1,18]
AFIELDS(laFileStr)
=gfCrtTmp(lcTempFile,@laFileStr,"Account+IIF(Type='S','1','2')+Style",lcTempFile,.F.)
  
*!**************************************************************************
*! Name      : lfCollectData
*! Developer : Mariam Mazhar (MMT)
*! Date      : 07/15/2008
*! Purpose   : Collect data
*!**************************************************************************
FUNCTION lfCollectData


llExtendedSizeScale = gfGetMemVar('M_USEEXSSC')
IF llExtendedSizeScale 
  STORE 0 TO lnColLen ,lnColPos 
  DECLARE laItemSeg[1]
  =gfItemMask(@laItemSeg)
  STORE 0 TO lnLenth , lnClrLen , lnClrPos
  FOR lnCount = 1 TO ALEN(laItemSeg,1)
    IF laItemSeg[lnCount,1]='C'
      lnColLen = LEN(laItemSeg[lnCount,3])
      lnColPos = laItemSeg[lnCount,4]
      EXIT
    ENDIF
  ENDFOR
ENDIF   



llAccount = .F.
lcAccFile = ''
lnAccPos = ASCAN(LOOGSCROLL.laOgFxFlt,'CUSTOMER.ACCOUNT')
IF lnAccPos  > 0
  lnAccPos   = ASUBSCRIPT(LOOGSCROLL.laOgFxFlt,lnAccPos  ,1)
  IF !Empty(LOOGSCROLL.laOgFxFlt[lnAccPos,6]) AND USED(LOOGSCROLL.laOgFxFlt[lnAccPos,6]) AND RECCOUNT(LOOGSCROLL.laOgFxFlt[lnAccPos,6]) > 0
    lcAccFile = LOOGSCROLL.laOgFxFlt[lnAccPos,6]
    llAccount = .T.
  ENDIF 
ENDIF   
 
llStyle = .F.
lcStyFile = ''
lnStyPos = ASCAN(LOOGSCROLL.laOgFxFlt,'STYLE.STYLE')
IF lnStyPos > 0
  lnStyPos  = ASUBSCRIPT(LOOGSCROLL.laOgFxFlt,lnStyPos,1)
  IF !Empty(LOOGSCROLL.laOgFxFlt[lnStyPos,6]) AND USED(LOOGSCROLL.laOgFxFlt[lnStyPos,6]) AND RECCOUNT(LOOGSCROLL.laOgFxFlt[lnStyPos,6]) > 0
    lcStyFile = LOOGSCROLL.laOgFxFlt[lnStyPos,6]
    llStyle = .T.
  ENDIF 
ENDIF   

llSKU = .F.
lcSKUFile = ''
lnSKUPos = ASCAN(LOOGSCROLL.laOgFxFlt,'SPCK_HDR.SKU')
IF lnSKUPos > 0
  lnSKUPos  = ASUBSCRIPT(LOOGSCROLL.laOgFxFlt,lnSKUPos ,1)
  IF !Empty(LOOGSCROLL.laOgFxFlt[lnSKUPos ,6]) AND USED(LOOGSCROLL.laOgFxFlt[lnSKUPos ,6]) AND RECCOUNT(LOOGSCROLL.laOgFxFlt[lnSKUPos ,6]) > 0
    lcSKUFile = LOOGSCROLL.laOgFxFlt[lnSKUPos ,6]
    llSKU = .T.
  ENDIF 
ENDIF   


IF llAccount  && In case of user selected account
  SELECT 'Spck_lin'
  gfSetOrder('Spck_lin') 
  SELECT(lcAccFile)
  SCAN 
    IF !gfSeek('S' + &lcAccFile..Account,'Spck_lin') AND !gfSeek('P' + &lcAccFile..Account,'Spck_lin') 
      LOOP   
    ENDIF  
    SELECT 'Spck_lin'
    IF gfSeek('S' + &lcAccFile..Account,'Spck_lin') 
      SELECT 'Spck_lin'
      SCAN REST WHILE TYPE+ACCOUNT+PACK_ID+STYLE+DYELOT = 'S' + &lcAccFile..Account FOR ;
        IIF(llStyle,SEEK(Spck_lin.Style,lcStyFile),.T.) 
        IF llSKU  
          IF (!gfSeek('S'+Spck_lin.Account+IIF(llExtendedSizeScale ,SUBSTR(Spck_lin.Style,1,lnColLen +lnColPos-1),Spck_lin.Style),'SPCK_HDR','SKU_STYLE')) OR ;
             (gfSeek('S'+Spck_lin.Account+IIF(llExtendedSizeScale ,SUBSTR(Spck_lin.Style,1,lnColLen +lnColPos-1),Spck_lin.Style),'SPCK_HDR','SKU_STYLE') AND !SEEK(SPCK_HDR.PACK_ID+"_"+SPCK_HDR.Account,lcSKUFile))
            LOOP
          ENDIF 
        ENDIF 
        gfSeek(spck_lin.Style,'Style')
        gfSeek('S'+Style.Scale,'Scale')
        lfAdd2Tmp()
      ENDSCAN 
    ENDIF
    
    IF !llSKU AND gfSeek('P' + &lcAccFile..Account,'Spck_lin') 
      SELECT 'Spck_lin'
      IF gfSeek('P' + &lcAccFile..Account,'Spck_lin') 
        SELECT 'Spck_lin'
        SCAN REST WHILE TYPE+ACCOUNT+PACK_ID+STYLE+DYELOT = 'P' + &lcAccFile..Account FOR ;
           IIF(llStyle,SEEK(Spck_lin.Style,lcStyFile),.T.) 
          gfSeek(spck_lin.Style,'Style')
          gfSeek('S'+Style.Scale,'Scale')
          lfAdd2Tmp()
        ENDSCAN 
      ENDIF
    ENDIF 
  ENDSCAN
ELSE
  IF llStyle  &&In case of user selected Style
    SELECT 'Spck_lin'
    gfSetOrder('SPCKLNST') &&TYPE+STYLE+ACCOUNT+PACK_ID
    SELECT (lcStyFile)
    SCAN 
      IF !gfSeek('S'+&lcStyFile..Style,'Spck_lin') AND !gfSeek('P'+&lcStyFile..Style,'Spck_lin')
        LOOP 
      ENDIF 
      IF gfSeek('S'+&lcStyFile..Style,'Spck_lin') 
        SELECT 'Spck_lin'
        SCAN REST WHILE TYPE+STYLE+ACCOUNT+PACK_ID = 'S'+&lcStyFile..Style 
          IF llSKU 
            IF (!gfSeek('S'+Spck_lin.Account+IIF(llExtendedSizeScale ,SUBSTR(Spck_lin.Style,1,lnColLen +lnColPos-1),Spck_lin.Style),'SPCK_HDR','SKU_STYLE')) OR ;
              (gfSeek('S'+Spck_lin.Account+IIF(llExtendedSizeScale ,SUBSTR(Spck_lin.Style,1,lnColLen +lnColPos-1),Spck_lin.Style),'SPCK_HDR','SKU_STYLE') AND !SEEK(SPCK_HDR.PACK_ID+"_"+SPCK_HDR.Account,lcSKUFile))
              LOOP
            ENDIF 
          ENDIF 
          gfSeek(spck_lin.Style,'Style')
          gfSeek('S'+Style.Scale,'Scale')
          lfAdd2Tmp()
        ENDSCAN 
      ENDIF 
      
      IF !llSKU AND  gfSeek('P'+&lcStyFile..Style,'Spck_lin')
        SELECT 'Spck_lin'
        SCAN REST WHILE TYPE+STYLE+ACCOUNT+PACK_ID = 'P'+&lcStyFile..Style 
          gfSeek(spck_lin.Style,'Style')
          gfSeek('S'+Style.Scale,'Scale')
          lfAdd2Tmp()
        ENDSCAN 
      ENDIF 
    ENDSCAN 
  ELSE
    SELECT 'Spck_lin'
    gfSetOrder('Spck_lin') 
    IF gfSeek('S')
      SCAN REST WHILE TYPE+ACCOUNT+PACK_ID+STYLE+DYELOT = 'S' 
        IF llSKU 
         IF (!gfSeek('S'+Spck_lin.Account+IIF(llExtendedSizeScale ,SUBSTR(Spck_lin.Style,1,lnColLen +lnColPos-1),Spck_lin.Style),'SPCK_HDR','SKU_STYLE')) OR ;
             (gfSeek('S'+Spck_lin.Account+IIF(llExtendedSizeScale ,SUBSTR(Spck_lin.Style,1,lnColLen +lnColPos-1),Spck_lin.Style),'SPCK_HDR','SKU_STYLE') AND !SEEK(SPCK_HDR.PACK_ID+"_"+SPCK_HDR.Account,lcSKUFile))
            LOOP
          ENDIF 
        ENDIF   
        gfSeek(spck_lin.Style,'Style')
        gfSeek('S'+Style.Scale,'Scale')
        lfAdd2Tmp()
      ENDSCAN
    ENDIF 
    IF !llSKU AND gfSeek('P')
       SCAN REST WHILE TYPE+ACCOUNT+PACK_ID+STYLE+DYELOT = 'P' 
        gfSeek(spck_lin.Style,'Style')
        gfSeek('S'+Style.Scale,'Scale')
        lfAdd2Tmp()
      ENDSCAN
    ENDIF 
  ENDIF 
ENDIF 

