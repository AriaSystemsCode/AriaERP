PROCEDURE mcheckseg
PARAMETERS lcValue , lcOldValue

SELECT (This.lcTmpAcct)
GO TOP
IF !EMPTY(cSegvalue)
  *** This modification will cause one or more  ***GLLSACT
  *** accounts to be without a type. ***
  *** < Show > < Cancel > < Edit > ***
  lnOption = gfModalGen("QRM02002B02001","DIALOG")
  DO CASE
    *-- Case of displaying the accounts
    CASE lnOption = 1
      *-- Browse all the segments that overlap with 
      *-- the new value in 'Segment List Screen'
      &lcValue = &lcOldValue
      DO FORM (oAriaApplication.ScreenHome+oAriaApplication.ActiveModuleID+"\GLLSACT.SCX") WITH Thisformset.lcTmpacct 
    *-- Case of cancel
    CASE lnOption = 2
      &lcValue = &lcOldValue
    *-- Case of editting
    CASE lnOption = 3
      &lcValue  = ""
      SELECT GLTYPES
      RETURN .F.
  ENDCASE
ENDIF
SELECT GLTYPES

ENDPROC
PROCEDURE mlowupr
PARAMETERS lcValue , lcOldValue
PRIVATE lcValue

*** Validate that no range has zero value ***
IF VAL(&lcValue) = 0
  *** Ranges cannot be zero !!! ***
  *** <   Ok   > ***
  = gfModalGen("TRM02041B00000","DIALOG")
  &lcValue = &lcOldValue
  RETURN .F.
ENDIF
*** Validate that the upper limit is higher than the lower limit ***
IF (!EMPTY(This.AriaForm1.txtLower.Value) AND ;
    !EMPTY(This.AriaForm1.txtUpper.Value) AND ;
    This.AriaForm1.txtLower.Value > This.AriaForm1.txtUpper.Value)
  *** Upper limit range should be greater ***
  *** than the lower limit range.         ***
  *** <   Ok   > ***
  = gfModalGen("INM02003B00000","DIALOG"," ")
  &lcValue = &lcOldValue
  RETURN .F.
ENDIF

ENDPROC
PROCEDURE recordchanged
LPARAMETERS tnSourceAction

DODEFAULT()

*-- Refresh the form to activate the current record values.
This.refresh

WITH This.AriaForm1
  .cboAccType.Value  = SUBSTR(.txtTypCode.Value,1,1)
  .cboAccType.Refresh
  .txtTypCode1.Value = SUBSTR(.txtTypCode.Value,1,1)
  .txtTypCode1.Refresh
  .txtTypCode2.Value = SUBSTR(.txtTypCode.Value,2,2)
  .txtTypCode2.Refresh
  This.lcOldlower = .txtLower.Value
  This.lcOldupper = .txtUpper.Value
ENDWITH

ENDPROC
PROCEDURE delete
*-- If it is a main type remove its description from the popup
IF This.AriaForm1.txtTypCode2.Value = '00'
  This.laConTyp[AT(This.AriaForm1.txtTypCode1.Value,CONS_Codes) ,1] = SUBSTR(This.laConTyp[AT(This.AriaForm1.txtTypCode1.Value,CONS_Codes) ,1],1,13)
ENDIF

IF DODEFAULT()
  SELECT GLTYPES
  lnRec = IIF(RECNO()>RECCOUNT(),0,RECNO())
  *-- So if delete a type & the types file is empty, set the used 
  *-- flag to .F. in the account code structure file 
  GO TOP IN GLTYPES
  IF EOF()
    SELECT ACCOD
    IF !EOF()
      REPLACE lAcsused WITH .F.
    ENDIF
  ENDIF
  SELECT GLTYPES
  IF lnRec <> 0
    GO lnRec
  ENDIF
ENDIF


ENDPROC
PROCEDURE changemode
lParameters lcModeToChange

DODEFAULT(lcModeToChange)

DO CASE 
  *-- If Select Mode
  CASE This.Activemode = "S"
    *-- Initialize the needed values
    WITH This.AriaForm1
      .cboAccType.Value    = This.laContyp[1,2]
      .cboAccType.Enabled  = .T.
      .txtTypCode1.Value   = .cboAccType.Value
      .txtTypCode2.Enabled = .T.
    ENDWITH 
  *-- If view mode
  CASE This.Activemode = "V"
    *-- Adjust the type code value.
    WITH This.AriaForm1
      .cboAccType.Value  = SUBSTR(.txtTypCode.Value,1,1)
      .cboAccType.Refresh 
      .txtTypCode1.Value = SUBSTR(.txtTypCode.Value,1,1)
      .txtTypCode1.Refresh
      .txtTypCode2.Value = SUBSTR(.txtTypCode.Value,2,2)
      .txtTypCode2.Refresh
      This.lcOldlower = .txtLower.Value
      This.lcOldupper = .txtUpper.Value
    ENDWITH
  *-- If edit mode
  CASE This.Activemode = "E"
    *-- Adjust the enabling/disabling of the form controls.
    WITH This.AriaForm1
      .cboAccType.Enabled  = .F.
      .txtTypCode2.Enabled = .F.
      IF SUBSTR(.txtTypCode.Value,2,2) = "00"
        .txtLower.Enabled = .F.
        .txtUpper.Enabled = .F.
      ENDIF
      This.lcOldlower = .txtLower.Value
      This.lcOldupper = .txtUpper.Value
    ENDWITH
  *-- If add mode
  CASE This.Activemode = "A"
    *-- Adjust the enabling/disabling of the form controls.
    WITH This.AriaForm1
      .cboAccType.Enabled  = .F.
      .txtTypCode2.Enabled = .F.
      IF SUBSTR(.txtTypCode.Value,2,2) = "00"
        .txtLower.Enabled = .F.
        .txtUpper.Enabled = .F.
      ENDIF
    ENDWITH 
ENDCASE


ENDPROC
PROCEDURE Init
*-- Check if the account code structure was entered for this company.
IF !This.AriaForm1.Gl1.gfcheckacc()
  RETURN .F.
ENDIF

DECLARE This.laConTyp [9,2]   && Combine between laAccTyp & Desc. of main types.
DECLARE laDescrip     [1,2]   && Hold the desc. of the main types

*-- Define the array of account types.
DECLARE laAccTyp   [9,1]
laAccTyp[1,1] = LANG_Asset
laAccTyp[2,1] = LANG_Cost
laAccTyp[3,1] = LANG_Equity
laAccTyp[4,1] = LANG_Income
laAccTyp[5,1] = LANG_Liabilty
laAccTyp[6,1] = LANG_Sales
laAccTyp[7,1] = LANG_Taxes
laAccTyp[8,1] = LANG_Expens
laAccTyp[9,1] = LANG_Stat

*-- Select all desc. of the main types ***
SELECT GLTYPES
SELECT cTypecode,cTypedesc FROM GLTYPES ;
       WHERE SUBSTR(cTypeCode,2,2) = '00';
        INTO ARRAY laDescrip

DIMENSION laDescrip[9,2],laDescTyp[9,2]
=ACOPY(laDescrip,laDescTyp)
=gfADel(@laDescTyp,2,2)
This.laConTyp = ' '
*-- Combine the 2 arrays(laAccTyp & laDescTyp) in one array for the popup.
FOR lnCount = 1 TO 9
  This.laConTyp [lnCount,1] = laAccTyp[lnCount] + ;
  SPACE(19-LEN(ALLTRIM(laAccTyp[lnCount]))) + '   ' + ;
  IIF(ASCAN(laDescTyp,SUBSTR(CONS_Codes,lnCount,1))>0 , ;
  laDescrip[ASCAN(laDescTyp,SUBSTR(CONS_Codes,lnCount,1)),2] , ' ')
  This.laConTyp [lnCount,2] = SUBSTR(CONS_Codes,lnCount,1)
ENDFOR

*-- Assign the first array value to the types popup.
This.AriaForm1.cboAccType.Value  = This.laConTyp [1,2]
This.AriaForm1.txtTypCode1.Value = This.laConTyp [1,2]

*-- Define default values to the properties that will hold the old lower & the old upper.
This.lcOldlower = ''
This.lcOldupper = ''
This.lnSegwidth = 0

*-- Save the width of lower or upper from the width of 1st seg. ***
SELECT ACCOD
GO TOP 
IF !EOF()
  SKIP
  This.lnSegWidth = ACCOD.NACSSIZE
ENDIF

*-- Adjust the width & the input mask of the lower range
This.AriaForm1.txtLower.InputMask = REPLICATE("9",This.lnSegWidth)
This.AriaForm1.txtLower.Format    = This.AriaForm1.txtLower.InputMask 
This.AriaForm1.txtLower.Width     = (9*(This.lnSegWidth)+10)

*-- Adjust the width & the input mask of the upper range
This.AriaForm1.txtUpper.InputMask = REPLICATE("9",This.lnSegWidth)
This.AriaForm1.txtUpper.Format    = This.AriaForm1.txtUpper.InputMask 
This.AriaForm1.txtUpper.Width     = (9*(This.lnSegWidth)+10)

This.lcTmpAcct =gfTempName()

*-- Assign alias to use it to open the GLTYPES in another work area
This.lcGlTypes = "lcGlTypes"
=gfOpenFile(oAriaApplication.DataDir+'GLTYPES' , oAriaApplication.DataDir+'Typecode' , 'SH' ,This.lcGlTypes)

This.Browsetitle = LANG_Types

DODEFAULT()

ENDPROC
PROCEDURE beforesave
*-- If it is not a main type & empty of lower or upper limit 
*-- accounts return again to enter them.
IF This.AriaForm1.txtTypCode2.Value <> '00' .AND.;
   (EMPTY(This.AriaForm1.txtLower.Value) .OR. EMPTY(This.AriaForm1.txtUpper.Value))
  *** You cannot save type with empty ranges !!! ***
  *** <   Ok   > ***
  = gfModalGen("INM02021B00000","DIALOG")
  RETURN .F.
ENDIF

ENDPROC
PROCEDURE savefiles
LPARAMETERS llCalledFromDelete

SELECT GLTYPES
IF This.AriaForm1.txtTypCode2.Value = '00'
  *-- If saving main type, put its desc. in the popup.
  IF This.Activemode = "E"
    This.laConTyp[AT(This.AriaForm1.txtTypCode1.Value,CONS_Codes) ,1] = ALLTRIM(LEFT(This.laConTyp[AT(This.AriaForm1.txtTypCode1.Value,CONS_Codes) ,1],22))
  ENDIF
  This.laConTyp[AT(This.AriaForm1.txtTypCode1.Value,CONS_Codes) ,1]   = ALLTRIM(This.laConTyp[AT(This.AriaForm1.txtTypCode1.Value,CONS_Codes) ,1]) + SPACE(22-LEN(ALLTRIM(This.laConTyp[AT(This.AriaForm1.txtTypCode1.Value,CONS_Codes) ,1]))) + ALLTRIM(This.AriaForm1.txtTypDesc.Value)
ENDIF

IF This.AriaForm1.txtTypCode2.Value <> '00' .AND. This.Activemode = "E"
  *-- Check if change lower limit, if there is
  *-- any types overlap with the new value.
  IF This.lcOldlower < This.AriaForm1.txtLower.Value
    *-- Select from the segment values file all the segment
    *-- that between the old lower limit & the new value.
    SELECT GLSEGVAL
    SELECT cSegvalue,cSeglndes FROM GLSEGVAL;
           WHERE cAcssegno = "1" .AND.  ;
                 cTypecode = ALLTRIM(This.AriaForm1.txtTypCode.Value) .AND.;
                 BETWEEN(VAL(cSegvalue),VAL(This.lcOldlower),VAL(This.AriaForm1.txtLower.Value)-1);
           INTO DBF (oAriaApplication.WorkDir+Thisformset.lcTmpAcct)
    
    SELECT (This.lcTmpAcct)
    IF !EMPTY(cSegvalue)
      *** This modification will cause one or more ***
      *** accounts to be without a type. ***
      *** <   Ok   > ***
      lnOption = gfModalGen("QRM02002B00000","DIALOG")
      SELECT GLTYPES
      *-- Set the field value to  the old  value has
      *-- been save in the view mode before editing.
      This.AriaForm1.txtLower.Value = This.lcOldlower 
    ENDIF
    SELECT GLTYPES
  ENDIF
  IF This.lcOldupper > This.AriaForm1.txtUpper.Value
    *-- Select from the segment values file all the segment
    *-- that between the old upper limit & the new value.
    SELECT GLSEGVAL
    SELECT cSegvalue,cSeglndes FROM GLSEGVAL;
           WHERE cAcssegno = "1" .AND.  ;
                 cTypecode = ALLTRIM(This.AriaForm1.txtTypCode.Value) .AND.;
                 BETWEEN(VAL(cSegvalue),VAL(This.AriaForm1.txtUpper.Value)+1,VAL(This.lcOldupper));
           INTO DBF (oAriaApplication.WorkDir+Thisformset.lcTmpAcct)
    SELECT (This.lcTmpAcct)
    IF !EMPTY(cSegvalue)
      *** This modification will cause one or more ***
      *** accounts to be without a type. ***
      *** <   Ok   > ***
      lnOption = gfModalGen("QRM02002B00000","DIALOG")
      SELECT GLTYPES
      *** Set the field value to  the old  value has ***
      *** been save in the view mode before editing. ***
      This.AriaForm1.txtUpper.Value = This.lcOldupper
    ENDIF
    SELECT GLTYPES
  ENDIF
ENDIF
IF This.AriaForm1.txtTypCode2.Value <> '00'
  SELECT lcGlTypes
  *-- Check if there is no ranges overlap the values in the new
  *-- record before saving to avoid any overlap can happen if
  *-- there is 2 user saving in the same time
  LOCATE FOR ALLTRIM(This.AriaForm1.txtTypCode.Value) <> cTypeCode .AND.;
             (BETWEEN(VAL(cTypuacno),VAL(This.AriaForm1.txtLower.Value),VAL(This.AriaForm1.txtUpper.Value)) .OR. ;
             BETWEEN(VAL(cTyplacno),VAL(This.AriaForm1.txtLower.Value),VAL(This.AriaForm1.txtUpper.Value)))
  IF FOUND()
    *** Ranges are overlapped for type ð. ***
    *** <   Ok   > ***
    =gfModalGen("QRM02001B00000","DIALOG",ALLTRIM(lcGlTypes.cTypshdes))
    RETURN .F.
  ENDIF
ENDIF
*-- This flag used by the system to allow  the user to modify the existing segments 
*-- in the  account code structure since the types' ranges have not been defined yet
*-- So if add new type, set this flag to .T.
SELECT ACCOD
IF !EOF()
  IF !ACCOD.lAcsused
    REPLACE lAcsused WITH .T.
  ENDIF
ENDIF
SELECT GLTYPES

*-- Put the standard field value according to the type (standard "Y" - statistical "N")
This.AriaForm1.txtStnadard.Value = IIF(SUBSTR(This.AriaForm1.txtTypCode.Value,1,1) = 'Y' , 'N' , 'Y')

*-- Call the global save.
DODEFAULT(llCalledFromDelete)

ENDPROC
PROCEDURE beforedelete
*-- Check that there is no segments will be without
*-- types if this deletion happen.... 
SELECT GLSEGVAL
SELECT cSegvalue,cSeglndes FROM GLSEGVAL;
       WHERE BETWEEN(VAL(cSegvalue),VAL(This.AriaForm1.txtLower.Value),VAL(This.AriaForm1.txtUpper.Value));
             .AND. cAcssegno = "1" .AND.  ;
             cTypecode = ALLTRIM(This.AriaForm1.txtTypCode.Value);
       INTO DBF (oAriaApplication.WorkDir+Thisformset.lcTmpAcct)

IF USED(This.lcTmpAcct)
  SELECT (This.lcTmpAcct)
  IF !EMPTY(cSegvalue)
    *** You cannot delete this type.         ***
    *** This Deletion will cause one or more ***
    *** accounts to be without a type. ***
    *** <  Ok  > ***
    lnOption = gfModalGen("QRM02197B00000","DIALOG")
    SELECT GLTYPES
    RETURN .F.
  ENDIF
ENDIF

ENDPROC
